settings for dialog {
	parameters for global defaults { alignment BL }
	parameters for label SELF { entity "%SELF%" }
	parameters for label PLAYER {
		entity "%PLAYER%"
		name "Sammy"
		alignment BR
	}
}

show_dialog-bender-glitched {
	show dialog {
		SELF emote 1
		"I speak binary fluently, kid! A little trick like this won't slow me down!"
	}
	set entity "%SELF%" interactScript to show_dialog-bender-start
}

show_dialog-bender-start {
	if entity "%SELF%" is glitched then goto show_dialog-bender-glitched
	copy set_handler-bender-off
	copy face-player
	if flag storyflag-bender is true then goto show_dialog-bender-end-s
	if entity Bender type is bender then goto show_dialog-bender-end
	if flag bender-backstory is true then goto show_dialog-bender-eval
	if entity Bender type is bender_sadbutt then goto show_dialog-bender-backstory
	goto show_dialog-bender-meatbag
}

show_dialog-bender-meatbag {
	show dialog {
		SELF name "????"
		SELF "Hey, you! Bite my --\nAhhh!"
		"Aaaargh, this is terrible! My ass is even more missing than before!"
		"First someone bit it off, and now it's an entirely different kind of ass!"
		"I feel like this is your fault somehow!"
		"Hurry up and fix it, kid!"
	}
	goto wrapup_dialog-bender-start
}

show_dialog-bender-backstory {
	show dialog {
		PLAYER "Oh! You look friendly!"
		SELF "Well, I'm not!"
	}
	copy bite-my-shiny-metal-ass-sad
	show dialog {
		SELF "Aww, man... I forgot! Someone bit my shiny metal ass!"
		"Why would someone do such a horrible thing to an innocent robot?!"
		"Life's not the same without my ass! How else am I supposed to taunt everyone stupid-looking?"
	}
	if entity "%PLAYER%" type is bender_sadbutt then goto show_dialog-bender-start-alt
	if entity "%PLAYER%" type is bender then goto show_dialog-bender-start-alt
	show dialog {
		SELF "Hey, you, kid! You're the one with the magic whatsit!"
		"Can't you fix this with your whatever powers?"
		"I'll give you a great reward! Just get my ass back!"
	}
	goto wrapup_dialog-bender-start
}

show_dialog-bender-start-alt {
	show dialog {
		SELF "Hey, you, strangely-handsome-looking kid! You're the one with the magic whatsit!"
		"Can't you fix this with your whatever powers?"
		"I'll give you a great reward! Just get my ass back!"
	}
	goto wrapup_dialog-bender-start
}

wrapup_dialog-bender-start {
	set flag bender-backstory to true
	copy set-hint-bender
	copy set_handler-bender-on
	set entity "%SELF%" interactScript to show_dialog-bender-start
}

show_dialog-bender-eval {
	if entity Bender type is bender_sadbutt then goto show_dialog-bender-start-s
	if entity Bender type is bender then goto show_dialog-bender-end
	show dialog {
		SELF "Come on! That doesn't look anything like me, half-assed or full-assed."
	}
	goto wrapup_dialog-bender-start
}

show_dialog-bender-start-s {
	copy bite-my-shiny-metal-ass-sad
	show dialog {
		SELF "...Oh, right. My ass. Someone bit it off."
		"So what's the holdup? I know you can fix it!"
		"I'll reward you! Just hurry it up!"
	}
	goto wrapup_dialog-bender-start
}

show_dialog-bender-end {
	if flag bender-backstory is false then goto show_dialog-bender-end-surprise
	show dialog {
		SELF "Hey, you did it! My ass is back!"
	}
	copy bite-my-shiny-metal-ass
	show dialog {
		SELF emote 1 "Hee hee hee!"
		"Hey, everyone! %Bender%'s ass is back!"
		PLAYER "...."
		SELF "...."
		PLAYER "Well?"
		SELF "Well, what?"
		PLAYER "You said you'd give me a reward!"
		SELF "I did! You got a front row seat to me, %Bender%, showing off my shiny metal ass!"
		"That's the greatest reward there is!"
		PLAYER "I don't know why I assumed you'd be friendly."
		SELF "Well, you know what they say about assumptions."
		SELF emote 1 "They make a shiny ass... and they give it to me, %Bender%."
	}
	goto bender-wrapup-eval
}

bender-wrapup-eval {
	set flag bender-backstory to true
	set flag storyflag-bender to true
	copy calculate-hint-tracking-series
	if variable hint-tracking-check is 5 then goto reset-bender-hint-only
		// if the hint series is the same, reset hint
	goto bender-success-wrapup
}

reset-bender-hint-only {
	copy set-hint-none
	goto bender-success-wrapup
}

bender-success-wrapup {
	goto show_dialog-bender-end-wrapup
}

show_dialog-bender-end-wrapup {
	copy set_handler-bender-none
	set entity "%SELF%" interactScript to show_dialog-bender-start
}

show_dialog-bender-end-s {
	if entity Bender type is bender then goto show_dialog-bender-satisfied
	show dialog {
		SELF "Yeah, yeah, you're a real comedian, kid. Now turn me back!"
	}
	goto show_dialog-bender-end-wrapup
}

show_dialog-bender-end-surprise {
	show dialog {
		SELF "Hey, my ass! It's back!"
	}
	copy bite-my-shiny-metal-ass
	show dialog {
		SELF emote 1
		"Hee hee hee!"
		"Hey, everyone! %Bender%'s ass is back!"

		SELF
		"...."
		"Don't look so smug, kid. It had nothing to do with you."
		"I'm sure my ass came home on its own!"
	}
	goto bender-wrapup-eval
}

show_dialog-bender-satisfied {
	copy bite-my-shiny-metal-ass
	show dialog {
		SELF emote 1 "Ooh, yeah!"
		PLAYER "It's shiny. I'll give you that."
	}
	goto show_dialog-bender-end-wrapup
}

bite-my-shiny-metal-ass {
	set player control to off
	wait 300ms
	play entity "%SELF%" animation 3 once
	set player control to on
}

bite-my-shiny-metal-ass-sad {
	set player control to off
	wait 300ms
	play entity "%SELF%" animation 3 once
	set player control to on
}

on_tick-bender-loiter {
	turn entity "%SELF%" south
	wait 1200ms
	turn entity "%SELF%" east
	wait 1400ms
	turn entity "%SELF%" north
	wait 2000ms
}

set_handler-bender-off {
	set entity "Bob Austin" tickScript to null_script
	set entity Bender tickScript to null_script
}

set_handler-bender-on {
	set entity Bender tickScript to face-player
	set entity "Bob Austin" tickScript to handler-bender-timer
}

set_handler-bender-none {
	set entity "Bob Austin" tickScript to null_script
	set entity Bender tickScript to on_tick-bender-loiter
}

handler-bender-timer {
	wait 2800ms
	play entity Bender animation 2 once
	wait 3900ms
	play entity Bender animation 2 once
}
